
// Establish version and status
ext.githubProjectName = 'spectator'

buildscript {
  repositories {
    mavenLocal()
    // http://stackoverflow.com/questions/26675814/gradle-could-not-head-https-pom-peer-not-authenticated
    maven { url "http://repo1.maven.org/maven2" }
    jcenter { url "http://jcenter.bintray.com/" }
  }
  apply from: file('gradle/buildscript.gradle'), to: buildscript 
}

allprojects {
  apply plugin: 'idea'
  apply plugin: 'project-report'
  apply plugin: 'me.champeau.gradle.jmh'
  apply plugin: 'com.github.ben-manes.versions'

  repositories {
    mavenLocal()
    maven { url "http://repo1.maven.org/maven2" }
  }
}

apply from: file('gradle/convention.gradle')
apply from: file('gradle/maven.gradle')
apply from: file('gradle/check.gradle')
apply from: file('gradle/license.gradle')
apply from: file('gradle/release.gradle')

subprojects {
  apply plugin: 'java'
  apply plugin: 'jacoco'

  group = "com.netflix.${githubProjectName}"

  sourceCompatibility = 1.7
  targetCompatibility = 1.7

  compileJava { 
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation" 
  }

  if (JavaVersion.current().isJava8()) {
    tasks.withType(Javadoc) {
      // http://blog.joda.org/2014/02/turning-off-doclint-in-jdk-8-javadoc.html
      options.addStringOption('Xdoclint:none', '-quiet')
    }
  }

  javadoc {
    options {
      links = ['http://docs.oracle.com/javase/7/docs/api/']
    }
  }

  dependencies {
    compile 'org.slf4j:slf4j-api:1.7.9'
    testCompile 'junit:junit:4.12'
    testCompile 'nl.jqno.equalsverifier:equalsverifier:1.5.1'
    jmh 'org.slf4j:slf4j-simple:1.7.9'
  }

  jmh {
    warmupIterations = 2
    iterations = 10
    fork = 5
    profilers = ['STACK']
    include '.*'
  }

  jacoco {
    toolVersion = "0.7.1.201405082137"
  }

  jacocoTestReport {
    additionalSourceDirs = files(sourceSets.main.allJava.srcDirs)
    reports {
      xml.enabled false
      csv.enabled false
      html.destination "${buildDir}/reports/jacoco"
    }
  }

  task copyDepsToBuild << {
    ['compile', 'runtime', 'testCompile', 'testRuntime'].each { conf ->
      delete "${buildDir}/dependencies/${conf}"
      copy {
        from configurations[conf]
        into "${buildDir}/dependencies/${conf}"
      }
    }
  }
}

